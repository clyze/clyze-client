plugins {
    id 'groovy'
    id 'application'
    id 'maven-publish'
    id 'net.researchgate.release' version '2.6.0'
}

//applicationDefaultJvmArgs = ["-Xmx2048m"]
sourceCompatibility = "1.8"
targetCompatibility = "1.8"

project.group = "org.clyze"

applicationName = project.name
mainClassName = 'org.clyze.client.cli.Main'

task wrapper(type: Wrapper) {
    gradleVersion = '4.9'
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "${artifactory_contextUrl}/plast-public" }
    maven {
        credentials {
            username artifactory_user
            password artifactory_password
        }

        url "${artifactory_contextUrl}/plast-private"
    }
}

configurations.all {
    resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
}

def doopVersion = "4.+"

dependencies {    
    compile "org.clyze:clue-common:3.8.+",                      // Clue common            
            "org.codehaus.groovy:groovy-all:2.5.5",             // Groovy
            "commons-logging:commons-logging:1.1",              // Logging wrapper
            "log4j:log4j:1.2.14",                               // Logging implementation
            "commons-io:commons-io:2.4",                        // IO utils
            "commons-cli:commons-cli:1.2",                      // Command line processor
            "org.apache.httpcomponents:httpclient:4.4",         // Used by the Rest client
            "org.apache.httpcomponents:httpmime:4.4"            // Used by the Rest client
    compile group: 'com.googlecode.juniversalchardet', name: 'juniversalchardet', version: '1.0.3'
}

run {
    //runs the Rest client
    if(project.hasProperty("args")) {
        args project.property("args").split()
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    repositories {
        maven {
            credentials {
                username artifactory_user
                password artifactory_password
            }

            url version.endsWith("-SNAPSHOT") ?
                "${artifactory_contextUrl}/libs-snapshot-local" :
                "${artifactory_contextUrl}/libs-release-local"
        }
    }
}

release {
    //use the defaults, see https://github.com/researchgate/gradle-release
}

afterReleaseBuild.dependsOn publish
