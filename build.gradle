buildscript {
    repositories {
	    jcenter()
    }
    dependencies {
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4+"
    }
}

apply plugin: 'groovy'
apply plugin: 'application'
apply plugin: 'com.jfrog.artifactory'
apply plugin: 'maven-publish'

//applicationDefaultJvmArgs = ["-Xmx2048m"]
//sourceCompatibility = "1.7"
//targetCompatibility = "1.7"

project.group = "org.clyze"
project.version ='1.0-SNAPSHOT'

applicationName = 'client'
mainClassName = 'org.clyze.doop.web.client.Main'

task wrapper(type: Wrapper) {
    gradleVersion = '2.14'
}

repositories {
    mavenCentral()
}

configurations.compile.resolutionStrategy.cacheChangingModulesFor(0, 'seconds')

dependencies {
    compile ("org.clyze:doop-nexgen:1.0-SNAPSHOT") {            // The doop core
        changing = true
    }

    compile "org.codehaus.groovy:groovy-all:2.4.7",             // Groovy
            "commons-logging:commons-logging:1.1",              // Logging wrapper
            "log4j:log4j:1.2.14",                               // Logging implementation
            "commons-cli:commons-cli:1.2",                      // Command line processor
            "org.apache.httpcomponents:httpclient:4.4",         // Used by the Rest client
            "org.apache.httpcomponents:httpmime:4.4"            // Used by the Rest client
}

run {
    //runs the Rest client
    if(project.hasProperty("args")) {
        args project.property("args").split()
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

artifactory {
    contextUrl = "${artifactory_contextUrl}"
    publish {
        repository {
            repoKey = 'libs-snapshot-local'
            username = "${artifactory_user}"
            password = "${artifactory_password}"
            maven = true

        }
        defaults {
            publications 'mavenJava'
        }
    }
    resolve {
        repository {
            repoKey = 'libs-snapshot'
            username = "${artifactory_user}"
            password = "${artifactory_password}"
            maven = true
        }
    }
}


