plugins {
    id 'groovy'
    id 'application'
    id 'maven-publish'
    id 'net.researchgate.release' version '2.4.0' 
}

//applicationDefaultJvmArgs = ["-Xmx2048m"]
sourceCompatibility = "1.8"
targetCompatibility = "1.8"

project.group = "org.clyze"

applicationName = project.name
mainClassName = 'org.clyze.doop.web.client.Main'

task wrapper(type: Wrapper) {
    gradleVersion = '3.1'
}

repositories {

    mavenLocal()

    mavenCentral()

    maven {
        credentials {
            username artifactory_user
            password artifactory_password
        }

        url "${artifactory_contextUrl}/plast"
    }
}

//This is required only for the SNAPSHOT dependencies
//configurations.compile.resolutionStrategy.cacheChangingModulesFor(0, 'seconds')

dependencies {
    //We should be cautious to declare clue-common as a direct dependency when it is used directly
    compile "org.clyze:clue-common:1.+",                        // Clue common
            "org.clyze:doop:2.9.+",                             // The doop core
            "org.codehaus.groovy:groovy-all:2.4.7",             // Groovy
            "commons-logging:commons-logging:1.1",              // Logging wrapper
            "log4j:log4j:1.2.14",                               // Logging implementation
            "commons-cli:commons-cli:1.2",                      // Command line processor
            "org.apache.httpcomponents:httpclient:4.4",         // Used by the Rest client
            "org.apache.httpcomponents:httpmime:4.4"            // Used by the Rest client
}

run {
    //runs the Rest client
    if(project.hasProperty("args")) {
        args project.property("args").split()
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    repositories {
        maven {
            credentials {
                username artifactory_user
                password artifactory_password
            }

            url version.endsWith("-SNAPSHOT") ?
                "${artifactory_contextUrl}/libs-snapshot-local" :
                "${artifactory_contextUrl}/libs-release-local"
        }
    }
}

release {
    //use the defaults, see https://github.com/researchgate/gradle-release
}

afterReleaseBuild.dependsOn publish
