plugins {
    id 'java-library'
    id 'groovy'
    id 'application'
    id 'maven-publish'
    id 'net.researchgate.release' version '2.6.0'
}

//applicationDefaultJvmArgs = ["-Xmx2048m"]
sourceCompatibility = "1.8"
targetCompatibility = "1.8"

project.group = "com.clyze"

applicationName = project.name
mainClassName = 'com.clyze.client.cli.Main'

wrapper {
    gradleVersion = '7.3.3'
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://clyze.jfrog.io/artifactory/default-maven-local' }
//    maven {
//        url "http://centauri.di.uoa.gr:8081/artifactory/plast-public"
//        allowInsecureProtocol true
//    }
//    // Experimental repositories for custom dependencies.
//    if (project.hasProperty('artifactory_contextUrl')) {
//        maven {
//            credentials {
//                username artifactory_user
//                password artifactory_password
//            }
//
//            url "${artifactory_contextUrl}/plast-private"
//            allowInsecureProtocol true
//        }
//    }
}

configurations.all {
    resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
}

def groovyVersion='3.0.9'
dependencies {
    api('org.clyze:clue-common:3.25.2') {
        exclude group: 'org.codehaus.groovy', module: 'groovy-xml'
    }
    api 'org.clyze:metadata-model:2.4.0'
    api 'commons-cli:commons-cli:1.4'
    api "org.codehaus.groovy:groovy:${groovyVersion}"
    api "org.codehaus.groovy:groovy-cli-commons:${groovyVersion}"
    api 'org.apache.httpcomponents:httpclient:4.5.13'   // Used by the Rest client, Apache 2.0
    api 'org.apache.httpcomponents:httpmime:4.5.13'     // Used by the Rest client, Apache 2.0
}

/**
 * Checks for dependencies that may come from mavenLocal and can harm
 * build reproducibility (e.g. for releases).
 */
project.afterEvaluate {
    configurations.default
	    .collect { it.canonicalPath }
	.findAll { it.contains('.m2') }
	.each { println "WARNING: build may be using mavenLocal(): ${it}" }
}

jar {
    manifest {
        //noinspection GrDeprecatedAPIUsage
        attributes 'Implementation-Version' : version
    }
}

run {
    //runs the Rest client
    if (project.hasProperty("args")) {
        args project.property("args").split()
    }
}

java {
    withSourcesJar()
    withJavadocJar()
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}
if (project.hasProperty('artifactory_user')) {
publishing {
    repositories {
        maven {
            credentials {
                username artifactory_user
                password artifactory_password
            }
            //Always publish to the public releases repo
            url "${artifactory_contextUrl}/libs-public-release-local"
        }
    }
}
}

release {
    git {
        // Permit releasing from all branches.
        requireBranch = null
    }
    // Use the defaults for the rest, see https://github.com/researchgate/gradle-release
}

afterReleaseBuild.dependsOn publish
